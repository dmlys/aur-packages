# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>
# added by me: -DBUILD_SHARED_LIBS=OFF to build static libs

pkgname=mingw-w64-fmt
pkgver=7.1.3
pkgrel=4
pkgdesc="{fmt} is an open-source formatting library for C++. It can be used as a safe and fast alternative to (s)printf and iostreams. (mingw-w64)"
url="https://fmt.dev/"
license=("MIT")
depends=("mingw-w64-crt")
makedepends=("mingw-w64-cmake")
arch=("any")
options=(!strip !buildflags staticlibs)
optdepends=()
sha256sums=(
	"SKIP"
)
source=(
	"https://github.com/fmtlib/fmt/archive/${pkgver}.tar.gz"
)

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
_flags=( -Wno-dev -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=OFF -DCMAKE_CXX_FLAGS_RELEASE="-O2 -DNDEBUG" -DFMT_DOC=OFF )

prepare() {
        cd "fmt-${pkgver}"
        sed -i 's/class ostream final/class FMT_API ostream final/' 'include/fmt/os.h'
}

build() {
	for _arch in ${_architectures}; do
		${_arch}-cmake -S "fmt-${pkgver}" -B "build-${_arch}" "${_flags[@]}" -DFMT_TEST=OFF
		make -C "build-${_arch}"
	done
}

check() {
        for _arch in ${_architectures}; do
                ${_arch}-cmake -S "fmt-${pkgver}" -B "build-${_arch}" "${_flags[@]}" -DFMT_TEST=ON
                cmake --build "build-${_arch}"
                cmake --build "build-${_arch}" --target test
        done
}


package() {
	for _arch in ${_architectures}; do
		make DESTDIR="${pkgdir}" -C "build-${_arch}" install
		#${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
	done
}
